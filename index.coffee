###
Convert heading text into anchor link, using the algorithm from
https://github.com/jch/html-pipeline/blob/master/lib/html/pipeline/toc_filter.rb
as described in
https://gist.github.com/asabaylus/3071099
###
headingToAnchor = (heading, headings) ->
  heading = heading
  .toLowerCase()
  .replace /[^\w\- ]/g, ''
  .replace /[ ]/g, '-'
  if headings[heading]
    heading += "-#{headings[heading]++}"
  else
    headings[heading] = 1
  heading

module.exports =
  names: ['github-internal-links']
  description: 'Check internal links like those generated by Github'
  tags: ['links']
  function: ({name, tokens, config}, onError) ->
    links = []
    heading = null
    headings = {}
    for token in tokens
      switch token.type
        when 'inline'
          for child in token.children
            switch child.type
              when 'link_open'
                for [key, value] in child.attrs
                  if key == 'href' and value?.startsWith '#'
                    links.push
                      anchor: value[1..]
                      lineNumber: child.lineNumber
              when 'text'
                heading += child.content if heading?
        when 'heading_open'
          heading = ''
          headingLineNumber = token.lineNumber
        when 'heading_close'
          anchor = headingToAnchor heading, headings
          if config.verbose
            console.log "#{name}:#{headingLineNumber} github-internal-links ##{anchor} = #{heading}"
          heading = null
    for {anchor, lineNumber} in links
      unless anchor of headings
        onError
          lineNumber: lineNumber
          detail: 'Internal anchor link does not match any heading'
          context: "##{anchor}"
      else if config.verbose
        console.log "#{name}:#{lineNumber} github-internal-links Link ##{anchor} OK"
    undefined
